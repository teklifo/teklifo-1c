#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет коммерческое предложение на основании данных запроса цен от клиента.
Процедура ЗаполнитьНаОснованииЗапросаЦенОтКлиента() Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	TK_ЗапросЦенОтКлиента.Организация КАК Организация,
						  |	TK_ЗапросЦенОтКлиента.Партнер КАК Партнер,
						  |	TK_ЗапросЦенОтКлиента.Контрагент КАК Контрагент,
						  |	TK_ЗапросЦенОтКлиента.Валюта КАК Валюта,
						  |	TK_ЗапросЦенОтКлиента.Товары.(
						  |		ИдентификаторСтроки КАК ИдентификаторСтрокиЗапроса,
						  |		ИдентификаторВерсии КАК ИдентификаторВерсииСтрокиЗапроса,
						  |		Номенклатура КАК ЗапрошеннаяНоменклатура,
						  |		НоменклатураТекстом КАК ЗапрошеннаяНоменклатураТекстом,
						  |		Количество КАК ЗапрошенноеКоличество,
						  |		МаксимальнаяЦена КАК ЗапрошеннаяЦена,
						  |		ДатаДоставки КАК ЗапрошеннаяДатаДоставки,
						  |		Комментарий КАК КомментарийЗапроса) КАК Товары
						  |ИЗ
						  |	Документ.TK_ЗапросЦенОтКлиента КАК TK_ЗапросЦенОтКлиента
						  |ГДЕ
						  |	TK_ЗапросЦенОтКлиента.Ссылка = &ЗапросЦенОтКлиента");
	Запрос.УстановитьПараметр("ЗапросЦенОтКлиента", ЗапросЦенОтКлиента);

	ВыборкаШапка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Если ВыборкаШапка.Следующий() Тогда

		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);

		Товары.Очистить();
		ВыборкаТовары = ВыборкаШапка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СтрокаТабличнойЧасти = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаТовары);
			СтрокаТабличнойЧасти.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры



// Заполнить имя контактного лица.
Процедура ЗаполнитьИмяКонтактногоЛица() Экспорт
	ИмяКонтактногоЛица = Строка(Менеджер);
КонецПроцедуры

// Заполнить контактные данные.
Процедура ЗаполнитьКонтактныеДанные() Экспорт
	КонтактныеДанныеОрганизации = TK_ОбщегоНазначенияВызовСервера.ПолучитьТелефонИEmailОрганизации(Организация);
	НомерТелефона = КонтактныеДанныеОрганизации.НомерТелефона;
	Email         = КонтактныеДанныеОрганизации.Email;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Менеджер = Пользователи.ТекущийПользователь();

	ЗаполнитьИмяКонтактногоЛица();
    
     
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.TK_ЗапросЦенОтКлиента") Тогда

		ЗапросЦенОтКлиента = ДанныеЗаполнения;
		ЗаполнитьНаОснованииЗапросаЦенОтКлиента();

	КонецЕсли;
  
   ЗаполнитьКонтактныеДанные();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Для Каждого СтрокаТабличнойЧасти Из Товары Цикл

		Если СтрокаТабличнойЧасти.Пропустить Тогда
			Продолжить;
		КонецЕсли;

		ИндексСтроки = СтрокаТабличнойЧасти.НомерСтроки - 1;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("Номенклатура", НСтр("ru = 'Номенклатура'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("Количество", НСтр("ru = 'Количество'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Цена) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("Цена", НСтр("ru = 'Цена'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сумма) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("Сумма", НСтр("ru = 'Сумма'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("СтавкаНДС", НСтр("ru = 'СтавкаНДС'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.СуммаСНДС) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("СуммаСНДС", НСтр("ru = 'СуммаСНДС'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДатаДоставки) Тогда
			СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти("ДатаДоставки", НСтр("ru = 'ДатаДоставки'"), ИндексСтроки);
			Отказ = Истина;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("СуммаСНДС");

	Если ЭтоНовый() Тогда
		ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ПустаяСсылка();
		ДокументИзменен = Ложь;
	Иначе
		ТекущийСтатус = TK_ОбщегоНазначенияВызовСервера.СтатусПубликацииКоммерческогоПредложенияКлиенту(Ссылка);
		ДокументИзменен = TK_ОбщегоНазначенияВызовСервера.КоммерческоеПредложениеКлиентуИзменено(ЭтотОбъект);
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ТекущийСтатус", ТекущийСтатус);
	ДополнительныеСвойства.Вставить("ДокументИзменен", ДокументИзменен);

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ПустаяСсылка();
	ДокументИзменен = Ложь;

	ДополнительныеСвойства.Свойство("ТекущийСтатус", ТекущийСтатус);
	ДополнительныеСвойства.Свойство("ДокументИзменен", ДокументИзменен);

	Если Не ЗначениеЗаполнено(ТекущийСтатус) Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииКоммерческогоПредложенияКлиенту(Ссылка,
			Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.НеОпубликован);
	ИначеЕсли ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.Опубликован И ДокументИзменен Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииКоммерческогоПредложенияКлиенту(Ссылка,
			Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ТребуетсяОбновление);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОбОшибкеЗаполненияСтрокиТабличнойЧасти(ИмяПоля, ПредставлениеПоля, ИндексСтроки)

	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено.'"), ПредставлениеПоля), ,
		СтрШаблон("Товары[%1].%2", ИндексСтроки, ИмяПоля), "Объект");

КонецПроцедуры

#КонецОбласти

#КонецЕсли