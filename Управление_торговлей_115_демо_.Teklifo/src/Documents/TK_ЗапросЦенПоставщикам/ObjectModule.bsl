#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполнить имя контактного лица.
Процедура ЗаполнитьИмяКонтактногоЛица() Экспорт
	ИмяКонтактногоЛица = Строка(Менеджер);
КонецПроцедуры

// Заполнить контактные данные.
Процедура ЗаполнитьКонтактныеДанные() Экспорт

	МассивВидов = Новый Массив;
	МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.EmailОрганизации);
	МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	МассивТипов.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	КонтИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Организация, МассивТипов,
		МассивВидов, ТекущаяДатаСеанса());

	НомерТелефона = "";
	Email         = "";

	Если КонтИнформация.Количество() > 0 Тогда
		ДанныеТелефона  = КонтИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации, "Вид");

		Если ДанныеТелефона <> Неопределено Тогда
			НомерТелефона = ДанныеТелефона.Представление;
		КонецЕсли;
		ДанныеEmailОрганизации  = КонтИнформация.Найти(Справочники.ВидыКонтактнойИнформации.EmailОрганизации, "Вид");

		Если ДанныеEmailОрганизации <> Неопределено Тогда
			Email = ДанныеEmailОрганизации.Представление;
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Менеджер = Пользователи.ТекущийПользователь();
	ЗаполнитьИмяКонтактногоЛица();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНовый() Тогда
		ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ПустаяСсылка();
		ДокументИзменен = Ложь;
	Иначе
		ТекущийСтатус = TK_ОбщегоНазначенияВызовСервера.СтатусПубликацииЗапросаЦенПоставщикам(Ссылка);
		ДокументИзменен = TK_ОбщегоНазначенияВызовСервера.ЗапросЦенПоставщикамИзменен(ЭтотОбъект);
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ТекущийСтатус", ТекущийСтатус);
	ДополнительныеСвойства.Вставить("ДокументИзменен", ДокументИзменен);

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ПустаяСсылка();
	ДокументИзменен = Ложь;

	ДополнительныеСвойства.Свойство("ТекущийСтатус", ТекущийСтатус);
	ДополнительныеСвойства.Свойство("ДокументИзменен", ДокументИзменен);

	Если Не ЗначениеЗаполнено(ТекущийСтатус) Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииЗапросаЦенПоставщикам(Ссылка,
			Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.НеОпубликован);
	ИначеЕсли ТекущийСтатус = Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.Опубликован И ДокументИзменен Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииЗапросаЦенПоставщикам(Ссылка,
			Перечисления.TK_СтатусыПубликацииДокументовНаTeklifo.ТребуетсяОбновление);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти





#КонецЕсли