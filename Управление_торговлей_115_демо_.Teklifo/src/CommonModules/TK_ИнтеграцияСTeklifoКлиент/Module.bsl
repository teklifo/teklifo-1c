#Область ПрограммныйИнтерфейс

// Запускает процедуру публикации запроса цен поставщикам на Teklifo.
// 
// Параметры:
//  ЗапросЦенПоставщикам - ДокументСсылка.TK_ЗапросЦенПоставщикам - Запрос цен поставщикам
Процедура ОпубликоватьЗапросЦенПоставщикамНаTeklifo(ЗапросЦенПоставщикам) Экспорт

	ПоказатьВопрос(Новый ОписаниеОповещения("ОпубликоватьЗапросЦенПоставщикамНаTeklifoПродолжение", ЭтотОбъект,
		ЗапросЦенПоставщикам), НСтр(
		"ru = 'Опубликовать запрос цен на сервисе Teklifo?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

КонецПроцедуры

// Запускает процедуру публикации коммерческого предложения клиенту на Teklifo.
// 
// Параметры:
//  КоммерческоеПредложениеКлиенту - ДокументСсылка.TK_КоммерческоеПредложениеКлиенту -  Коммерческое предложение клиенту
Процедура ОпубликоватьКоммерческоеПредложениеКлиентуНаTeklifo(КоммерческоеПредложениеКлиенту) Экспорт

	ПоказатьВопрос(Новый ОписаниеОповещения("ОпубликоватьКоммерческоеПредложениеКлиентуНаTeklifoПродолжение", ЭтотОбъект,
		КоммерческоеПредложениеКлиенту), НСтр(
		"ru = 'Отправить коммерческое предложение клиенту?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпубликоватьЗапросЦенПоставщикамНаTeklifoПродолжение(Результат, ЗапросЦенПоставщикам) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	Результат = TK_ИнтеграцияСTeklifoВызовСервера.ОпубликоватьЗапросЦенПоставщикамНаTeklifo(
		ЗапросЦенПоставщикам);

	Если Результат Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииЗапросаЦенПоставщикам(
			ЗапросЦенПоставщикам, ПредопределенноеЗначение(
			"Перечисление.TK_СтатусыПубликацииДокументовНаTeklifo.Опубликован"));

		ОткрытьФорму("Документ.TK_ЗапросЦенПоставщикам.Форма.ФормаРезультатаПубликаци",
			Новый Структура("ДокументСсылка", ЗапросЦенПоставщикам));

	КонецЕсли;

	Оповестить("TK_ОбновлениеСтатусаПубликации", ЗапросЦенПоставщикам);

КонецПроцедуры

Процедура ОпубликоватьКоммерческоеПредложениеКлиентуНаTeklifoПродолжение(Результат, КоммерческоеПредложениеКлиенту) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	Результат = TK_ИнтеграцияСTeklifoВызовСервера.ОпубликоватьКоммерческоеПредложениеКлиентуНаTeklifo(
		КоммерческоеПредложениеКлиенту);

	Если Результат Тогда
		TK_ОбщегоНазначенияВызовСервера.УстановитьСтатусПубликацииКоммерческогоПредложенияКлиенту(
			КоммерческоеПредложениеКлиенту, ПредопределенноеЗначение(
			"Перечисление.TK_СтатусыПубликацииДокументовНаTeklifo.Опубликован"));

		ОткрытьФорму("Документ.TK_КоммерческоеПредложениеКлиенту.Форма.ФормаРезультатаПубликаци",
			Новый Структура("ДокументСсылка", КоммерческоеПредложениеКлиенту));

	КонецЕсли;

	Оповестить("TK_ОбновлениеСтатусаПубликации", КоммерческоеПредложениеКлиенту);

КонецПроцедуры

#КонецОбласти